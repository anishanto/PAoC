#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

vClientID=CellGetS('Global Control','Client Server Name','String');

sSQLDel = '  Delete from [DW_TM1_Budget].[ssdi].[FiscalMonth] where ClientID=''' |vClientID|''' ';
ODBCOPEN('BudgetWB','TM1_readonly','tm1readonly');
ODBCOutput('BudgetWB',sSQLDel); 

CubeName ='}ElementAttributes_'|pDim;
ViewNameTransfer =  'VE-' | CubeName | '-DW' ;
vDim='}ElementAttributes_'|pDim;

#  Destroy previous views and subsets for forecast transfer (in case they currently exist)
ViewDestroy (CubeName, ViewNameTransfer);
SubsetDestroy (pDim, ViewNameTransfer);
SubsetDestroy (vDim, ViewNameTransfer);

#Month Subset
sMDX = '{TM1SORT( {TM1FILTERBYLEVEL( { HIERARCHIZE( {TM1SUBSETALL( ['| pDim |'] )} ) }, 0)}, ASC)}';
SubsetCreatebyMDX( ViewNameTransfer, sMDX);

#Create }ElementAttributes_Months
SubsetCreate (vDim, ViewNameTransfer);
SubsetElementInsert ( vDim,  ViewNameTransfer, 'Name' , 1);

ViewCreate( CubeName, ViewNameTransfer);
ViewSubsetAssign (CubeName, ViewNameTransfer,pDim, ViewNameTransfer);
ViewSubsetAssign (CubeName, ViewNameTransfer,vDim, ViewNameTransfer);

# Set new views as the datasource:
DatasourceNameForServer = CubeName;
DatasourceCubeView = ViewNameTransfer;





#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****


vCubeName ='}ElementAttributes_'|pDim;
vClientID=CellgetS('Global Control','Client Server Name','String');
vName=CellGetS (vCubeName, Dim, 'Name' );
vLastUpdated=TODAY(1);

#Handling apostrophe(delimeter)
sName = '';
nCounter = Long ( vName );
While ( nCounter > 0 );
sCharacter = SubSt ( vName, nCounter, 1 );
If ( sCharacter @= '''' );
sCharacter = '''''';
EndIf;
sName = sCharacter | sName;
nCounter = nCounter - 1;
End;




sSQL = 'Insert into [DW_TM1_Budget].[ssdi].[FiscalMonth]  (ClientID,Month,MonthName,LastUpdated)
values (''' |vClientID|''',''' |Dim|''',''' |sName|''',''' |vLastUpdated|''')';
ODBCOutput('BudgetWB',sSQL); 



#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****

ODBCClose('BudgetWB');

ViewNameTransfer =  'VE-' | CubeName | '-DW' ;

#  Destroy previous views and subsets for forecast transfer
ViewDestroy (CubeName, ViewNameTransfer);
SubsetDestroy (pDim, ViewNameTransfer);
SubsetDestroy (vDim, ViewNameTransfer);
#endregion